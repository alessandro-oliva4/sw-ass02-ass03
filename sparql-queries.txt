PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

# count of CIS subclasses

SELECT (COUNT(?subCIS) AS ?NClasses)
WHERE {
?subCIS rdfs:subClassOf* cult:CulturalInstitutionOrSite .
}

# number of libraries

SELECT (COUNT(?library) as ?libraryCount)
WHERE {
?library rdf:type cult:Library .
}

# count of collections for each CIS

SELECT ?cis (COUNT(?collection) as ?collectionCount)
WHERE {
?collection rdf:type cult:Collections .
?cis rdf:type cult:CulturalInstitutionOrSite .
?cis cult:maintains ?collection .
}
GROUP BY ?cis

# collections with "Archivio" in their name

SELECT ?collection
WHERE {
?collection a cult:Collections;
	ext:name ?name .
filter( regex(str(?name), "Archivio" ))
}

# contact points with opening time after 8:00

SELECT ?contactPoint
WHERE {
?contactPoint a ext:ContactPoint;
	ext:available ?spec .
?spec a ext:OpeningHoursSpecification;
	ext:opens ?time.
FILTER ( ?time > "08:00:00"^^xsd:time )
}

# count of activies hosted for each site

SELECT ?site (COUNT(?activity) as ?activityCount)
WHERE {
?activity rdf:type cult:Activity .
?site ext:hosts ?activity .
}
GROUP BY ?site

# sites that contains collections with "Casa Piani" in their name

SELECT ?site
WHERE {
?collection a cult:Collections ;
	ext:name ?name .
?site a cult:Site;
	ext:isSiteOf ?collection .
FILTER( regex(str(?name), "Casa Piani" ))
}

# select CIS and related services (blank field if a CIS has no services)

SELECT ?cis ?service
WHERE {
?cis a cult:CulturalInstitutionOrSite .
OPTIONAL {
?cis ext:provides ?service .
}
}




# count how many times CIS "nome" has changed their "ruolo" since 2010
SELECT (COUNT(?o) AS ?totalNumberOfNames)
WHERE {
	?s cult:hasRole ?o .
	?o ext:roleFor 'nome' .
	?o ext:withRole 'ruolo' .
	?interval a ext:timeInterval;
		ext:startDate ?startdate .
	FILTER ( xsd:dateTime(?startDate) >= '2010-01-01'^^xsd:dateTime )
}

##### events hosted by "Biblioteca_Comunale_di_Imola"
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

SELECT DISTINCT ?event
WHERE {
	?cis a cult:CulturalInstitutionOrSite ;
		ext:name ?cisName .
	?event a ext:Event ;
		ext:hostedBy ?cis .
	FILTER ( ?cisName='Biblioteca Comunale di Imola'^^xsd:string )
}

# agents that are not CIS
SELECT ?entity
WHERE {
	?entity rdf:type ?subAgents.
	?subAgents rdfs:subClassOf* cult:CulturalInstitutionOrSite .
}

##### the event hosting the highest number of subevents
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

SELECT ?event (count(DISTINCT ?subEvent) as ?numSubEvents)
WHERE {
	?event a ext:Event ;
		ext:hasSubEvent ?subEvent
}
GROUP BY ?event
ORDER BY desc(?numSubEvents)
LIMIT 1

##### free tickets'
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

SELECT ?ticket
WHERE {
	?ticket a ext:Ticket .
	FILTER NOT EXISTS {
		?fee a ext:Fee .
		?fee ext:includes ?ticket .
	}
}
GROUP BY ?ticket

##### tickets' name and description that give access to event "Mostra miniature" or cis "Museo egizio Torino"
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

SELECT ?ticketName ?ticketDescription
WHERE {
	?ticket a ext:Ticket ;
		ext:name ?ticketName ;
		ext:description ?ticketDescription .
	{
		?ticket ext:canAccess ?event .
		?event a ext:Event ;
			ext:name ?eventName .
		FILTER ( ?eventName='Mostra miniature'^^xsd:string )
	} UNION {
		?ticket ext:canAccess ?cis .
		?cis a cult:CulturalInstitutionOrSite ;
			ext:name ?cisName .
		FILTER ( ?cisName='Museo egizio Torino'^^xsd:string )
	}
}
GROUP BY ?ticketName ?ticketDescription

##### fee with expired validity at date "2021-04-01"
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

SELECT ?fee ?end
WHERE {
	?fee a ext:Fee ;
		ext:hasValidity ?validity .
	?validity ext:hasDuration ?interval .
	?interval ext:endDate ?end .
	FILTER ( ?end < '2021-04-01T00:00:00Z'^^xsd:dateTime )
}
GROUP BY ?fee ?end

##### Ask if access condition with description "Prenotazione telefonica" exists
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cult: <http://culturalis.org/cult/0.1#>
PREFIX ext: <http://www.semanticweb.org/aless/ontologies/2021/4/culturalis-ext#>

ASK
WHERE {
	?s a ext:AccessCondition ;
		ext:description ?desc .
	FILTER ( ?desc = 'Prenotazione telefonica'^^xsd:string )
}